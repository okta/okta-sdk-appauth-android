apply plugin: "com.android.library"
apply from: "${rootDir}/gradle/okta-common.gradle"
apply from: "${rootDir}/gradle/android-common.gradle"
apply from: "${rootDir}/gradle/style.gradle"
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.github.dcendents.android-maven"
apply plugin: 'maven-publish'

group = "com.okta.android"
version = "${rootProject.versionName}"

android {

    defaultConfig {
        project.archivesBaseName = "appauth-android"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        main {
            java {
                srcDir "src/main/java"
                srcDir "${rootDir.absolutePath}/app-auth/AppAuth-Android/library/java"
            }
        }
        test {
            manifest {
                srcFile "${rootDir.absolutePath}/library/src/test/AndroidManifest.xml"
            }
        }
        androidTest {
            manifest {
                srcFile "${rootDir.absolutePath}/library/src/test/AndroidManifest.xml"
            }
        }
    }
}


buildscript {
   repositories {
       jcenter()
   }
   dependencies {
       classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${rootProject.bintrayVersion}"
       classpath "com.github.dcendents:android-maven-gradle-plugin:${rootProject.gradlePluginVersion}"
   }
}

dependencies {
    //used as a provider of manifest for app-auth
    //should be removed when singout functionality is merged
    implementation "com.squareup.okio:okio:${rootProject.okioVersion}"
    implementation "com.android.support:customtabs:${rootProject.browserVersion}"
    testImplementation "com.squareup.okhttp3:okhttp-tls:${rootProject.okhttpVersion}"
    testImplementation "io.jsonwebtoken:jjwt-api:${rootProject.jsonWebTokenVersion}"
    testImplementation "io.jsonwebtoken:jjwt-impl:${rootProject.jsonWebTokenVersion}"
    testImplementation "io.jsonwebtoken:jjwt-jackson:${rootProject.jsonWebTokenVersion}"
    testImplementation("io.jsonwebtoken:jjwt-orgjson:${rootProject.jsonWebTokenVersion}") {
        exclude group: 'org.json', module: 'json'
    }
    testImplementation "junit:junit:${rootProject.junitVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.mockitoVersion}"
    testImplementation "org.robolectric:robolectric:${rootProject.robolectricVersion}"
    testImplementation "com.squareup.assertj:assertj-android:${rootProject.assertjVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${rootProject.okhttpVersion}"
    testImplementation "com.squareup.okhttp3:okhttp:${rootProject.okhttpVersion}"
}

publishing {
    publications {
        Release(MavenPublication) {
            artifact("$buildDir/outputs/aar/appauth-android-release.aar")
            groupId "${group}"
            artifactId "${android.defaultConfig.project.archivesBaseName}"
            version "${rootProject.versionName}"
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    configurations = ["archives"]
    publications = ['Release']
    pkg {
        repo = "com.okta.android"
        name = "okta-sdk-appauth-android"
        desc = "Okta Authentication using AppAuth"
        userOrg = "okta"
        licenses = ["Apache-2.0"]
        websiteUrl = "https://github.com/okta/okta-sdk-appauth-android"
        vcsUrl = "https://github.com/okta/okta-sdk-appauth-android.git"
        issueTrackerUrl = "https://github.com/okta/okta-sdk-appauth-android/issues"
        version {
            name = "${rootProject.versionName}"
            desc = "Okta Authentication using AppAuth"
            vcsTag = "${rootProject.versionName}"
        }
    }
}
