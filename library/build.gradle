apply plugin: "com.android.library"

apply from: "${rootDir}/gradle/android-common.gradle"

group = "com.okta.android"
version = "${rootProject.versionName}"

task buildAppAuthLibrary(type: Exec) {
    commandLine 'sh', "${rootDir.absolutePath}/assemble-appauth.sh",
            "${rootDir.absolutePath}/app-auth/AppAuth-Android"
}
preBuild.dependsOn buildAppAuthLibrary

android {
    defaultConfig {
        project.archivesBaseName = "appauth-android"
        manifestPlaceholders = [
                "appAuthRedirectScheme": "com.okta.appauth.android.test"
        ]
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}


buildscript {
   repositories {
       jcenter()
   }
   dependencies {
       classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${rootProject.bintrayVersion}"
       classpath "com.github.dcendents:android-maven-gradle-plugin:${rootProject.gradlePluginVersion}"
   }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: "com.github.dcendents.android-maven"

dependencies {
    //Build of forked version of AppAuth-Android with end session functionality
    //will be removed when end session feature will be released
    compile fileTree(
            dir: rootProject.appAuthLocation ,
            include: rootProject.appAuthName
    )
    implementation "androidx.browser:browser:${rootProject.browserVersion}"
    implementation "com.squareup.okio:okio:${rootProject.okioVersion}"

    testImplementation "junit:junit:${rootProject.junitVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.mockitoVersion}"
    testImplementation "org.robolectric:robolectric:${rootProject.robolectricVersion}"
    testImplementation "com.squareup.assertj:assertj-android:${rootProject.assertjVersion}"
}

apply from: "${rootDir}/gradle/style.gradle"

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    configurations = ["archives"]
    pkg {
        repo = "com.okta.android"
        name = "okta-sdk-appauth-android"
        desc = "Okta Authentication using AppAuth"
        userOrg = "okta"
        licenses = ["Apache-2.0"]
        websiteUrl = "https://github.com/okta/okta-sdk-appauth-android"
        vcsUrl = "https://github.com/okta/okta-sdk-appauth-android.git"
        issueTrackerUrl = "https://github.com/okta/okta-sdk-appauth-android/issues"
        version {
            name = "${rootProject.versionName}"
            desc = "Okta Authentication using AppAuth"
            vcsTag = "${rootProject.versionName}"
        }
    }
}
